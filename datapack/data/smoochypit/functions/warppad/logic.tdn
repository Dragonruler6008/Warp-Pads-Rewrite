@ tag load
schedule function / 10t

at @a[
    tag=!wp.spam,
    tag=!global.ignore.pos,
    predicate=smoochypit:warppad/potential_warp
]
align xyz
    tag @e[type=$wp_pad,dx=0,dy=0,dz=0] add wp.active

as @e[tag=wp.active]
at @s
align xyz
unless entity @p[dx=0,dy=0,dz=0]
function {
    tag @s remove wp.active
    tag @s remove wp.processed
}

as @e[tag=wp.selector]
at @s
unless entity @e[tag=wp.active,distance=..5]
    kill @s

unless entity @e[tag=wp.active]
    set @e[type=$wp_pad]->wp.temp_ID = 0

as @a 
at @s
align xyz
unless entity @e[tag=wp.active,dx=0,dy=0,dz=0]
function {
    tag @s remove wp.pilot
    if entity @s[tag=wp.displaying_warp_gui] 
    function {
        tag @a[tag=wp.displaying_warp_gui] remove global.ignore.gui
        tag @a remove wp.displaying_warp_gui
    }
}

as @e[tag=wp.active,tag=!wp.processed]
at @s
function {
    tag @s add wp.processing
    align xyz
    tag @p[dx=0,dy=0,dz=0] add wp.pilot
    
    # Add wp.invaliddest to all within the corresponding list of UUIDS
    function /../array_check/invalid_dests
    
    # Add wp.validdest to all within the corresponding list of UUIDs
    function /../array_check/valid_dests
    
    # Check remaining $wp_pad s
    function /../conditions
    
    # Create icons
    function /../warping/icon_align
    
    # Create selector
    function /../warping/make_selector
    
    say Invalid : @e[tag=wp.invalid_dest]
    say Valid : @e[tag=wp.valid_dest]
    say Remaining: @e[type=$wp_pad,tag=!wp.valid_dest,tag=!wp.invalid_dest,tag=!wp.processing]
    
    tag @e remove wp.early_exclude
    tag @e remove wp.invalid_dest
    tag @e remove wp.valid_dest
    
    tag @s add wp.processed
    tag @s remove wp.processing
}
