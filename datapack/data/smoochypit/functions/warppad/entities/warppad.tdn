define objective wp.x
define objective wp.z
define objective wp.math
define objective wp.spawn_anim

define event wp_spawn_base_items

define entity wp_pad minecraft:marker {
    
    default name [
        "Warp Pad"
    ]
    
    default nbt {
        data:{
            Color:0b,
            Frequency:0b,
            Destructable:true,
            Reserved:false,
            Private:false,
            Owner_UUID:[
                I;
                0,
                0,
                0,
                0
            ]
        },
        Tags:[
            "wp.pad",
            "global.ignore",
            "global.ignore.pos",
            "global.forceload"
        ]
    }
    
    function construct {
        
        forceload add ~ ~
        
        data modify entity @s data.Frequency set value 0
        data modify entity @s data.Color set value 0
        
        store result score @s wp.x
            data get entity @s Pos[0] 1
        store result score @s wp.z
            data get entity @s Pos[2] 1
        
    }
    
    function destruct {
        
        particle witch ~ ~-1 ~ 0 0 0 0.5 25
        
        function /../chunk_unload
        
        positioned ~ ~-1 ~ function {
            
            unless entity @s[
                nbt={
                    data:{
                        Frequency:0
                    }
                }
            ]
            positioned ~ ~-1 ~
            function {
                summon item ~ ~ ~ {
                    Tags:["wp.freq_item"],
                    Item:{
                        id:${(string) warp_config.modifiers.frequency},
                        Count:1b
                    },
                    PickupDelay:25s}
                store result entity @e[tag=wp.freq_item,limit=1,sort=nearest] Item.Count byte 1.0
                    data get entity @s data.Frequency
            }
            
            if entity @s[
                nbt={
                    data:{
                        Private:true
                    }
                }
            ]
            positioned ~ ~-1 ~
            summon item ~ ~ ~ {
                Item:{
                    id:${(string) warp_config.modifiers.private},
                    Count:1b
                },
                PickupDelay:25s
            }
            
            if entity @s[
                nbt={
                    data:{
                        Reserved:true
                    }
                }
            ]
            positioned ~ ~-1 ~
                summon item ~ ~ ~ {
                Item:{
                    id:${(string) warp_config.modifiers.reserved},
                    Count:1b
                },
                PickupDelay:25s
            }
            
            event @s wp_spawn_base_items
            
        }
        
        kill @s
        
    }
    
    function chunk_unload { # as and at warp pad being destroyed
        
        tag @s remove global.forceload # for later comparison
        
        using summon marker with wp.locator {
            
            at @s expand {  # copied Initia_Nova's homework
                # Move x to chunk edge
                set #pos->wp.math = @s.Pos[0]
                set #pos->wp.math /= 16
                set @s.Pos[0] * 16.0 = #pos->wp.math
                # Move z to chunk edge
                set #pos->wp.math = @s.Pos[2]
                set #pos->wp.math /= 16
                set @s.Pos[2] * 16.0 = #pos->wp.math
                # Move y to -64
                set @s.Pos[1] = -64.0
                
                unless entity @e[tag=global.forceload,dx=16,dy=320,dz=16]
                    forceload remove ~ ~
                
            }
            
        }
        
    }
    
    function animate_per {
        
        as @e[tag=wp.pad,scores={wp.spawn_anim=1..}] function {
            at @s positioned ~ ~-1.4 ~ function /../../animate_items
            set @s->wp.spawn_anim -= 1
        }
        
        if entity @e[tag=wp.pad,scores={wp.spawn_anim=1..}]
        schedule function / 1t
        
    }
    
    function animate_items {
        
        if entity @s[scores={wp.spawn_anim=26..100}]
        particle portal ~ ~0.8 ~ 0.25 0 0.25 0.3 5
        
        if entity @s[scores={wp.spawn_anim=11..100}]
        as @e[tag=wp.primary_anim,distance=..2.5]
        at @s
        tp @s ~ ~0.0135 ~
        
        if entity @s[scores={wp.spawn_anim=11..50}]
        as @e[tag=wp.primary_anim,distance=..2.5]
        at @s
        expand {
            tp @s ^ ^ ^-0.2
            
            tp @s ^ ^ ^ ~18 ~ #720 degrees in 40 ticks
            
            tp @s ^ ^ ^0.2
        }
        
        if entity @s[scores={wp.spawn_anim=51..100}]
        as @e[tag=wp.primary_anim,distance=..2.5]
        at @s
        expand {
            tp @s ^ ^ ^-0.2
            
            tp @s ^ ^ ^ ~7.2 ~ #360 degrees in 50 ticks
            
            tp @s ^ ^ ^0.2
        }
        
        if entity @s[scores={wp.spawn_anim=1..10}]
        as @e[tag=wp.primary_anim,distance=..2.5]
        at @s
        expand {
            tp @s ^ ^ ^-0.2
            
            tp @s ~ ~-0.17 ~
            
            tp @s ^ ^ ^0.2
        }
        
        if entity @s[scores={wp.spawn_anim=51..100}]
        as @e[tag=wp.secondary_anim,distance=..2.5]
        at @s
        expand {
            tp @s ^ ^ ^-0.2
            
            tp @s ^ ^ ^0.02 ~4 ~
                
            tp @s ^ ^ ^0.2
        }
        
        if entity @s[scores={wp.spawn_anim=1}]
        as @e[tag=wp.primary_anim,distance=..2.5]
        at @s
        function {
            particle witch ~ ~1.4 ~ 0 0 0 1 80
            particle reverse_portal ~ ~1.4 ~ 0.1 0.1 0.1 1 80
            kill @s
        }
        
        if entity @s[scores={wp.spawn_anim=50}]
        as @e[tag=wp.secondary_anim,distance=..2.5]
        at @s
        function {
            particle witch ~ ~1.4 ~ 0 0 0 0 10
            kill @s
        }
    }

}

# Define an entity component "tier_*" for each tier in config

var blocks : list = []

var primaries : list = []

var secondaries : list = []

for (var i=0; i<warp_config.tiers.length; i++) {
    
    var tier = warp_config.tiers[i]
    
    eval blocks.add((resource) (string) tier.base)
    eval primaries.add((resource) (string) tier.primary)
    eval secondaries.add((resource) (string) tier.secondary)
    
    define entity component ${"tier_" + i} {
        
        on wp_spawn_base_items function {
            
            summon item ~ ~ ~ {
                PickupDelay:25s,
                Item:{
                    id:${(string) tier.primary},
                    Count:1b
                }
            }
            
            summon item ~ ~ ~ {
                PickupDelay:25s,
                Item:{
                    id:${(string) tier.secondary},
                    Count:4b
                }
            }
            
        }
        
    }
    
}

eval Tags.createTag(
    "block",
    resource</../../base_blocks>,
    blocks
)

eval Tags.createTag(
    "item",
    resource</../../primary_items>,
    primaries
)

eval Tags.createTag(
    "item",
    resource</../../secondary_items>,
    secondaries
)
